from snakemake.utils import min_version

min_version(
    "7.7.0"
)  # Snakemake 7.7.0 introduced `retries` directive used in fetch-sequences


# Utility functions shared across all workflows.
include: "../shared/vendored/snakemake/config.smk"


# Use default configuration values. Extend with Snakemake's --configfile/--config options.
configfile: os.path.join(workflow.basedir, "defaults/config.yaml")


# Use custom configuration from analysis directory (i.e. working dir), if any.
if os.path.exists("config.yaml"):

    configfile: "config.yaml"


# This is the default rule that Snakemake will run when there are no specified targets.
rule all:
    input:
        "results/sequences.fasta",
        "results/metadata.tsv",


include: "rules/curate.smk"
include: "rules/nextclade.smk"
include: "rules/fetch_from_ppx.smk"


if "custom_rules" in config:
    for rule_file in config["custom_rules"]:

        # Relative custom rule paths in the config are relative to the analysis
        # directory (i.e. the current working directory, or workdir, usually
        # given by --directory), but the "include" directive treats relative
        # paths as relative to the workflow (e.g. workflow.current_basedir).
        # Convert to an absolute path based on the analysis/current directory
        # to avoid this mismatch of expectations.
        include: os.path.join(os.getcwd(), rule_file)


rule extract_open_data:
    input:
        metadata="results/metadata.tsv",
        sequences="results/sequences.fasta",
    output:
        metadata="results/metadata_open.tsv",
        sequences="results/sequences_open.fasta",
    shell:
        """
        augur filter --metadata {input.metadata} \
                     --sequences {input.sequences} \
                     --metadata-id-columns PPX_accession \
                     --exclude-where "dataUseTerms=RESTRICTED" \
                     --output-metadata {output.metadata} \
                     --output-sequences {output.sequences}
        """
